# shows available recipes
help:
    @just -l

# get ansible-vault password from bitwarden
login:
    #!/bin/bash
    if [ "$(bw login --check --response | jq .success)" == "true" ]; then
    # if we are already logged in, then we can't get the session key
    # and getting the password will prompt for the password _again_.

        # so just logout
        bw logout
    fi
    export BW_SESSION=$(bw login --raw)
    touch .ansible-vault-password
    chmod 0600 .ansible-vault-password
    bw get password ansible-vault > .ansible-vault-password

# logout of bitwarden and remove ansible-vault password file
logout:
    rm .ansible-vault-password
    bw logout

# apply changes to inventory
apply:
    poetry run ansible-playbook --vault-password-file .ansible-vault-password -i inventory.yaml playbooks/site.yaml

# plan changes without modifying inventory
plan:
    poetry run ansible-playbook --vault-password-file .ansible-vault-password -i inventory.yaml playbooks/site.yaml --check

# encrypt a variable named {name} using ansible-vault
encrypt name:
    poetry run ansible-vault encrypt_string --vault-password-file .ansible-vault-password --stdin-name {{name}}
    @echo ""
